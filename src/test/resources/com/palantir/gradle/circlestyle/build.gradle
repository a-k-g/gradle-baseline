plugins {
  id 'java'
  id 'checkstyle'
  id 'findbugs'
}


// Apply CircleStylePlugin
String[] classpath = System.env.TEST_CLASSPATH.split(':')
URL[] urls = new URL[classpath.length]
for (int i = 0; i < classpath.length; ++i) {
  urls[i] = new File(classpath[i]).toURI().toURL()
}
ClassLoader testClassLoader = new URLClassLoader(urls, getClass().classLoader)
def pluginClass = testClassLoader.loadClass('com.palantir.gradle.circlestyle.CircleStylePlugin')
plugins.apply(pluginClass)


repositories {
  mavenCentral()
}

sourceCompatibility = 1.7

dependencies {
  testCompile 'junit:junit:4.12'
  testCompile 'org.assertj:assertj-core:2.9.0'
}

tasks.withType(Checkstyle) {
  reports {
    xml.enabled = false
    html.enabled = true
  }
}

findbugs {
  includeFilter = file("config/findbugs/findbugsIncludeFilter.xml")
}

tasks.withType(FindBugs) {
  reports {
    xml.enabled = false
    html.enabled = true
  }
}

task failingTask {
  doLast {
    throw new RuntimeException("This task will always fail")
  }
}
